
/*===================================================================================================
Project:			SAR Poverty micro-simulations - Preparing variables
Institution:		World Bank - ESAPV

Authors:			Sergio Olivieri & Kelly Y. Montoya
E-mail:				solivieri@worldbank.org; kmontoyamunoz@worldbank.org
Creation Date:		11/4/2024

Last Modification:	Kelly Y. Montoya (kmontoyamunoz@worldbank.org)
Modification date:  2/26/2026
===================================================================================================*/

* NOTE: Be careful: check by-one-bye

/*===================================================================================================
	1 - EMPLOYMENT STATUS
===================================================================================================*/

* Status
rename  lstatus_year lstatus_year_orig
gen     lstatus_year = lstatus
replace lstatus_year = 1 if  !inlist(ip,0,.) & "${country}" == "BGD"

* Employment/unemployment
gen emplyd 		= lstatus_year == 1 if welfare != . & lstatus != .
gen unemplyd 	= lstatus_year == 2 if welfare != . & lstatus != .

* Sample
cap drop sample
gen sample 		= age > 14 & age != .
cap clonevar id	= hhid

rename occup_year occup_year_orig
qui sum occup_year_orig
if r(N) == 0 gen occup_year = occup
else gen occup_year = occup_year_orig
			
* We are using informality here instead of skills
qui cap drop informal
qui gen informal = .
replace informal = socialsec == 0 if empstat == 1
replace informal = educat4 != 4 if educat4 != . & !inlist(empstat,1,.) & lstatus == 1
replace informal = . if occup_year == . & educat4 == .


/*===================================================================================================
	2 - INCOME VARIABLES
===================================================================================================*/

* Foreign remittances
egen itranext = rowtotal(itranext_m itranext_nm), m

* Domestic remittances
egen itranint = rowtotal(itranint_m itranint_nm), m

* Non-specified private remittances
gen itranp_ns = itran_ns

* deflate welfare // Needs to be fixed in SARMD and dlw
if "${country}${year}" == "BGD2022" {

			sum   zu_cbn [aw=wgt] 
			local mean_nat = r(mean)
			
			/*
			sum   welfare [aw=wgt] 
			local avg = r(mean)

			gen welfare_adj = welfare*`mean_nat'/zu_cbn
			sum welfare_adj [aw=wgt] 
			local avg2 = r(mean)
			replace welfare = welfare_adj*`avg'/`avg2'
			drop welfare_adj
			*/
			replace welfare = welfaredef
			
			replace pline_nat = pline_nat * (welfaredef / welfarenat)
					
		}

* Convert to real terms
foreach incomevar in welfare ila ijubi itranext itranint itranp_ns itranp itrane icap inla_otro inla renta_imp ipcf itf ip inp {
	cap drop `incomevar'_ppp
	gen `incomevar'_ppp = `incomevar' / cpi$ppp / icp$ppp
	replace `incomevar'_ppp = `incomevar'_ppp / 12 if ${year} == 2022 & "$country" == "BGD"
}

*replace welfare_ppp = welfare_ppp * 12
*replace ip_ppp = . if ip < 0
*replace ila_ppp = . if ila_ppp < 0

if $national == 0 {
	*Make sure this is total family income
	clonevar h_inc		= itf_ppp
	clonevar lai_m		= ip_ppp
	clonevar lai_s		= inp_ppp
	clonevar lai_orig	= ila_ppp
}


/*===================================================================================================
	3 -  LABOR MARKET VARIABLES
===================================================================================================*/

* Economic sectors
/* 1 "Agriculture, Hunting, Fishing, etc." 2 "Mining" 3 "Manufacturing" 4 "Public Utility Services" 5 "Construction" 6 "Commerce" 7 "Transport and Communications" 8 "Financial and Business Services" 9 "Public Administration" 10 "Others */
local sectors_vars "industrycat10 industrycat10_2"
foreach var of local sectors_vars {
	cap rename `var'_year `var'_year_orig
	qui sum `var'_year_orig
	if r(N) == 0 recode `var' (1=1 "Agriculture") (2 3 4 5 =2 "Industry") (6 7 8 9 10 =3 "Services") , gen(sector_`var')
	else qui recode `var'_year (1=1 "Agriculture") (2 3 4 5 =2 "Industry") (6 7 8 9 10 =3 "Services") , gen(sector_`var')
}

ren (sector_industrycat10 sector_industrycat10_2) (sect_main sect_secu)

* public job_status
rename ocusec_year ocusec_year_orig
qui sum ocusec_year_orig
if r(N) == 0 gen ocusec_year = ocusec
else gen ocusec_year = ocusec_year_orig
label values ocusec ocusec_year ocusec_year_orig ocusec

gen     public_job = 0 if lstatus == 1 & welfare != .
replace public_job = 1 if lstatus == 1 & welfare != . & ocusec_year == 1

note: by definiton the public job is part of formal services sector
replace sect_main  = 3 if !inlist(sect_main, 3) & public_job ==1 & sect_main!= .
*replace sect_main6 = 5 if !inlist(sect_main6,5) & public_job ==1 & sect_main6 != .

* Primary activity
gen     sect_main6 = .
replace sect_main6 = 1 if  sect_main == 1 & emplyd == 1 & informal == 0
replace sect_main6 = 2 if  sect_main == 1 & emplyd == 1 & informal == 1
replace sect_main6 = 3 if  sect_main == 2 & emplyd == 1 & informal == 0
replace sect_main6 = 4 if  sect_main == 2 & emplyd == 1 & informal == 1
replace sect_main6 = 5 if  sect_main == 3 & emplyd == 1 & informal == 0
replace sect_main6 = 6 if  sect_main == 3 & emplyd == 1 & informal == 1
label var sect_main6 "main economic sector" 

* secondary activity
gen     sect_secu6 = .
replace sect_secu6 = 1 if  sect_secu == 1 & emplyd == 1 & informal == 0
replace sect_secu6 = 2 if  sect_secu == 1 & emplyd == 1 & informal == 1
replace sect_secu6 = 3 if  sect_secu == 2 & emplyd == 1 & informal == 0
replace sect_secu6 = 4 if  sect_secu == 2 & emplyd == 1 & informal == 1
replace sect_secu6 = 5 if  sect_secu == 3 & emplyd == 1 & informal == 0
replace sect_secu6 = 6 if  sect_secu == 3 & emplyd == 1 & informal == 1
label var sect_secu6  "secondary economic sector" 

label def sectors         	///
1 "agriculture-formal"   	///
2 "agriculture-informal" 	///
3 "industry-formal"   		///
4 "industry-informal" 		///
5 "services-formal"   		///
6 "services-informal", replace
label values sect_main6 sect_secu6 sectors

* labor relationship
local relation_vars "empstat empstat_2"
foreach var of local relation_vars {
	rename `var'_year `var'_year_orig
	qui sum `var'_year_orig
	if r(N) == 0 gen `var'_year = `var'
	else gen `var'_year = `var'_year_orig
	label values `var'_year `var' `var'_year_orig `var'
}

gen salaried 	= empstat_year == 1 			if emplyd==1
gen self_emp 	= inlist(empstat_year,3,4) 		if emplyd==1 
gen unpaid 		= empstat_year == 2 			if emplyd==1

gen salaried2 	= empstat_2_year == 1 			if emplyd==1
gen self_emp2 	= inlist(empstat_2_year,3,4) 	if emplyd==1 
gen unpaid2		= empstat_2_year == 2 			if emplyd==1

* primary activity
gen     labor_rel = 1 if salaried == 1
replace labor_rel = 2 if self_emp == 1
replace labor_rel = 3 if unpaid   == 1
replace labor_rel = 4 if unemplyd == 1
label var labor_rel "labor relation-primary job"

* secondary activity
gen     labor_rel2 = 1 if salaried2 == 1
replace labor_rel2 = 2 if self_emp2 == 1
replace labor_rel2 = 3 if unpaid2   == 1
label var labor_rel2 "labor relation-secondary job"

label def lab_rel 	///
1 "salaried"   		///
2 "self-employd" 	///
3 "unpaid" 			///
4 "unemployed" ,replace
label values labor_rel labor_rel2 lab_rel


/*===================================================================================================
	4 - INCOME VARIABLES CHECK
===================================================================================================*/

* labor income
egen    tot_lai = rowtotal(lai_m lai_s), missing
replace tot_lai = lai_s if lai_m < 0
replace tot_lai = . if lai_orig == .
if abs(tot_lai - lai_orig) > 1 & abs(tot_lai - lai_orig) != . di in red "WARNING: Please check variables definition. tot_lai is different from lai_orig."
drop lai_orig

* total household labor incomes
egen h_lai = sum(tot_lai) if hogarsec != 1, by(id) missing

* Household size
clonevar h_size = hsize

* Non-labor incomes
gen capital_ppp		= icap_ppp
gen pensions_ppp	= ijubi_ppp
gen otherinla_ppp	= inla_otro_ppp
gen remitt_ppp		= itranp_ppp
gen int_remit_ppp	= itranext_ppp
gen dom_remit_ppp	= itranint_ppp
gen ns_remit_ppp	= itranp_ns_ppp
gen transfers_ppp	= itrane_ppp

if $national == 0 { 
	
	note: Imputed rent included
	replace renta_imp_ppp = renta_imp_ppp / h_size
	local var "capital_ppp pensions_ppp otherinla_ppp remitt_ppp int_remit_ppp dom_remit_ppp ns_remit_ppp renta_imp_ppp transfers_ppp"
	
	foreach x of local var {
		egen     h_`x' = sum(`x') if hogarsec != 1, by(id) missing
		replace  h_`x' = . if h_`x' == 0	
	}
} 

rename h_*_ppp h_*

* household income
egen mm = rowtotal(h_lai h_*_remit h_pensions h_capital h_renta_imp h_otherinla h_transfers)
replace mm = . if mm == 0 & h_inc == .
replace mm = 0 if mm < 0

gen resid = h_inc - mm
replace resid = 0 if resid < 0
drop mm

egen h_nlai = rowtotal(h_*_remit h_pensions h_capital h_renta_imp h_otherinla h_transfers resid), missing

* at household level 
local var "h_remitt h_int_remit h_dom_remit h_ns_remit h_pensions h_capital h_renta_imp h_otherinla h_nlai h_transfers resid"
foreach x of local var {
	replace  `x' = . if relationharm != 1
}
replace h_nlai   = . if h_nlai == 0


/*===================================================================================================
	5 - INDEPENDENT VARIABLES
=====================================================================================================
	gender:			    	male 
	experience:		    	age
	experience2:			age2
	education dummies:		none and infantil
							primary
							secundary
							superior
	household head:			h_head
	marital status:			married
	regional dummies:   	region	
	remittances:		    remitt_any
	other memb public job:	oth_pub
	dependency:		        depen
===================================================================================================*/
	
* education level (aggregate primary and none)
gen educ_lev = educat5 - 1
replace educ_lev = 1 if educ_lev ==0

if "$country" == "MDV" {
	cap drop educ_lev
	gen educ_lev = educat4 - 1
	replace educ_lev = 1 if educ_lev ==0
}

* household head
gen h_head = (relationharm == 1)

* marital status
gen married = (marital == 1) if marital != .

* remittances domestic or abroad
cap drop aux*
gen aux  = 1 if (remitt_ppp >0 & remitt_ppp!=.)
replace	   aux  = 0 if  aux ==. 
egen       remitt_any = max(aux), by(id)
label var  remitt_any "[=1] if household receives remittances"

* other member with public salaried job
cap drop aux*
egen 	aux 	= total(public_job), by(id)
gen     oth_pub = sign(aux - public_job)
replace oth_pub = sign(aux) if missing(public_job)
lab var oth_pub "[=1] if other member with public job"

* dependency ratio
cap drop aux*
cap drop depen
egen aux = total((age < 15 | age > 64)), by(id)
gen       depen = aux/h_size 
label var depen "potential dependency"

* log main labor income
gen ln_lai_m = ln(lai_m)

/*===================================================================================================
	6 - DEPENDENT VARIABLES
===================================================================================================*/

gen active = inlist(lstatus_year,1,2) if lstatus_year != .

gen     occupation = .
replace occupation = 0 if  active     == 0          	
replace occupation = 1 if  unemplyd   == 1	    	  	
replace occupation = 2 if  sect_main == 1 & emplyd == 1 & informal == 0
replace occupation = 3 if  sect_main == 1 & emplyd == 1 & informal == 1
replace occupation = 4 if  sect_main == 2 & emplyd == 1 & informal == 0
replace occupation = 5 if  sect_main == 2 & emplyd == 1 & informal == 1
replace occupation = 6 if  sect_main == 3 & emplyd == 1 & informal == 0
replace occupation = 7 if  sect_main == 3 & emplyd == 1 & informal == 1
label var occupation "occupation status"

label define occup 	///
0 "inactive" 		/// 
1 "unempl"   		///
2 "agr-for"  		/// 
3 "agr-inf" 		///
4 "ind-for" 			///
5 "ind-inf" 		///
6 "ser-for"  		///
7 "ser-inf", replace 
label values occupation occup
	
	
/*===================================================================================================
	7 - Setting up sample 
===================================================================================================*/

local var "ln_lai_m sect_main6 sect_main sect_secu6 sect_secu occupation"

foreach x of varlist `var' {
    replace `x' = . if sample != 1
}

* education variables
gen     sample_1 = 1 if educ_lev <  3 &  sample == 1 
lab var sample_1 "low educated"
gen     sample_2 = 1 if educ_lev >= 3 &  sample == 1 
lab var sample_2 "high educated"

gen     skill_edu = 1 if sample_1 == 1
replace skill_edu = 2 if sample_2 == 1

/*===================================================================================================
	- END
===================================================================================================*/
